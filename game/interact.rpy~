# each location is designed to hold one love interest, and so if you have another then "park" should be duplicated. the other labels in this file should be kept single, however.
    
label park:
    scene bg park
    show casey
    
    if (day > 30):
        $ renpy.hide_screen("townmap")
        "End game with Casey?"
        menu:
            "Yes":
                jump endA
            "No":
                jump map
    
    call relAcheck
    jump parkA
    
# i separate this from park solely so that the relationship upgrade scene doesnt replay when it shouldnt.
label parkA:
    scene bg park
    show casey
    # reset date tallies to default in case we end up going on one
    $ mood = 0
    $ talkcount = talkcountmax
    $ giftcount = giftcountmax
    $ photocount = photocountmax
    
    # these calculations determine the date variables. curbar uses the relationship status to determine progress to next date, assuming 100 points for each stage. if higher than 100, shows 100.
    $ curdate = "Casey"
    $ currel = Arel
    if (currel == "Stranger"):
        $ curnext = pointsfriend
        $ curbar = min(Alove, pointsfriend)
    if (currel == "Friend"):
        $ curnext = pointslove - pointsfriend
        $ curbar = min(Alove - pointsfriend, pointslove - pointsfriend)
    if (currel == "Lover"):
        $ curnext = pointssoul - pointslove
        $ curbar = min(Alove - pointslove, pointssoul - pointslove)
    if (currel == "Soulmate"):
        $ curnext = 100
        $ curbar = 100
    
    $ renpy.show_screen("mapchat")
    $ renpy.pause ()
    
# a simple little label that will handle the relationship upgrades when youre close enough.
label relAcheck:
    $ canwarp = False
    $ renpy.hide_screen("townmap")
    if (Alove >= pointsfriend) and (Arel == "Stranger"):
        $ Arel = "Friend"
        show side mc onlayer mcsprite at midleft
        c "Hey, [pName]. How are you doing?"
        c "You know, we've been talking for a while. I feel like we've gotten really close."
        c "I'm happy I've gotten to know you, [pName]. You're pretty cool!"
        hide side mc onlayer mcsprite
        "You are now 'Friends' with Casey. You can now give her gifts."
        
    if (Alove >= pointslove) and (Arel == "Friend"):
        $ Arel = "Lover"
        show side mc onlayer mcsprite at midleft
        c "H-hey, [pName]. How's it going?"
        c "You know, there's something I've been meaning to talk to you about."
        c "All this time we've been spending together... it means a lot to me."
        c "I might be getting ahead of myself, but... would you wanna go out sometime?"
        menu:
            "Yes":
                c "R-Really? Great! I'm so happy, [pName]."
            "Yes":
                c "R-Really? Great! I'm so happy, [pName]."
        hide side mc onlayer mcsprite
        "You are now dating Casey! You can now go on dates."
        
    if (Alove >= pointssoul) and (Arel == "Lover"):
        $ Arel = "Soulmate"
        show side mc onlayer mcsprite at midleft
        c "[pName]..."
        c "I don't know what to say. You've grown to mean so much to me."
        c "I wish I could ask you to stay by my side forever, but we both know that's impossible."
        c "Remember me, promise?"
        hide side mc onlayer mcsprite
        "You and Casey are now soulmates. Stick around until day 31 to get a special ending with her!"
        
    $ canwarp = True
    return
    
# if you have multiple love interests you can add an "if (curdate == ??)" to this label too, but if you have a lot of potential dialogue itll likely be more efficient to put each character in a new label and use this to jump to the right one.
label chattalk:
    $ renpy.hide_screen("mapchat")
    $ canwarp = False
    if (HP >= 10):
        $ HP -= 10
        show side mc at midleft onlayer mcsprite
        c "Hey! You're looking as soulless and vacant as usual. While I have you here... Are you free for a date on Tuesday? There's a really nice new coffee shop."
        menu:
            "Sure, I'll go out with you.":
                $ Alove += 10
                show loveup
                c "Wait, really? I thought you'd just brush me off! I'll make sure my schedule is free, then."
            "I'm busy Tuesday. And Wednesday. My schedule is pretty full.":
                c "Oh. Okay. Yeah, that's... fine, I guess. I'll ask you another time."
        hide side mc onlayer mcsprite
    else:
        "You don't have enough HP for that!"
    $ canwarp = True
    jump parkA
    
label chatgift:
    $ renpy.hide_screen("chatgift")
    if (currel == "Stranger"):
        if (curdate == "Casey"):
            c "Um, you're nice, [pName], but I don't think I'm comfortable with you buying me stuff just yet."
            jump parkA
    else:
        $ globals()[curgift] -= 1
        
        if (curdate == "Casey"):
            $ Alove += 10
            show loveup
            c "Oh! Thank you, [pName], this is great!"
            jump parkA
    
label chatdate:
    $ renpy.hide_screen("mapchat")
    if (currel == "Stranger") or (currel == "Friend"):
        if (curdate == "Casey"):
            c "Um... Sorry, I don't think today is a good day."
            jump parkA
    else:
        if (HP >= 50):
            $ HP -= 50
            if (curdate == "Casey"):
                c "H-Huh? You really want to? Great! Let's head out, then."
                jump dateA
        else:
            "You don't have enough HP for that!"
            jump parkA
